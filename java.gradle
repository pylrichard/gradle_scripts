apply plugin: 'java'
apply plugin: 'idea'

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.encoding = 'UTF-8'
            //关闭JDK1.8的doclint特性
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

//为了方便查看源码及调试，把源码上传到nexus仓库
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//为了方便查看注释，把javadoc上传到nexus仓库
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def createDir = {
    path ->
        File dir = new File(path)
        if (!dir.exists()) {
            dir.mkdirs()
        }
}

//此处右键执行Run
task makeDir() {
    def paths = ['src/main/java', 'src/main/resources', 'src/test/java', 'src/test/resources']
    doFirst {
        paths.forEach(createDir)
    }
}

task makeWebDir() {
    //任务配置，要定义在动作执行前
    dependsOn('makeDir')
    def paths = ['src/main/webapp']
    //任务执行
    //doLast可以被<<替换
    doLast {
        paths.forEach(createDir)
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}